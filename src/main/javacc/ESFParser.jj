/*======================================================================*
 * Copyright (c) 2008, Yahoo! Inc. All rights reserved.                 *
 *                                                                      *
 * Licensed under the New BSD License (the "License"); you may not use  *
 * this file except in compliance with the License.  Unless required    *
 * by applicable law or agreed to in writing, software distributed      *
 * under the License is distributed on an "AS IS" BASIS, WITHOUT        *
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.     *
 * See the License for the specific language governing permissions and  *
 * limitations under the License. See accompanying LICENSE file.        *
 *======================================================================*/

options {
  DEBUG_PARSER=false;
  DEBUG_LOOKAHEAD=false;
  DEBUG_TOKEN_MANAGER=false;
  STATIC=false;
}

PARSER_BEGIN(ESFParser)

package org.lwes.db;

import java.math.BigInteger;

import org.apache.commons.logging.Log;
import org.apache.commons.logging.LogFactory;

import org.lwes.FieldType;
import org.lwes.util.IPAddress;

public class ESFParser
{

    private static transient Log log = LogFactory.getLog(ESFParser.class);

  private String currentEvent;
  private EventTemplateDB eventTemplateDB;

  public void setEventTemplateDB(EventTemplateDB DB)
    { eventTemplateDB = DB; }

  public EventTemplateDB getEventTemplateDB()
    { return eventTemplateDB; }

  public void setCurrentEvent(String evt)
    { currentEvent = evt; }

  public String getCurrentEvent()
    { return currentEvent; }

  public String removeQuotes(String str) {
    return str.replace("\"","");
  }
}

PARSER_END(ESFParser)

SKIP :
{
  " "
| "\t"
| "\n"
| "\r"
| "#" : IN_LINE_COMMENT
}

<IN_LINE_COMMENT> SKIP:
{
   "\n" : DEFAULT
|  "\r" : DEFAULT
|  "\r\n" : DEFAULT
}

<IN_LINE_COMMENT> MORE:
{
  < ~[] >
}


TOKEN :
{
    < REQ: "required" >
  | < OPT: "optional" >
  | < TRUE: "true" >
  | < FALSE: "false" >
  | < LBRACKET: "[" >
  | < RBRACKET: "]" >
  | < BIG_INTEGER_LITERAL: ["1"-"9"]["0"-"9"]["0"-"9"]["0"-"9"]["0"-"9"]["0"-"9"]["0"-"9"]["0"-"9"]["0"-"9"]["0"-"9"] (["0"-"9"])* >
  | < INTEGER_LITERAL: ("-")? ["1"-"9"] (["0"-"9"])* >
  | < IPADDR_LITERAL:  (["0"-"9"])+ "." (["0"-"9"])+ "." (["0"-"9"])+ "." (["0"-"9"])+ >
  | < STRING_LITERAL: "\""(<ID>)"\"" >
  | < DOUBLE_LITERAL: ("-")? (["0"-"9"])+ "." (["0"-"9"])* >
  | < ID: ["a"-"z","A"-"Z","_",":","0"-"9","."] (["a"-"z","A"-"Z","_",":","0"-"9","."])* >

}

/**
 * A list of events
 */
void eventlist() :
{
}
{
  event() ( event() )* <EOF>
}

/**
 * a single event
 */
void event() :
{
}
{
  eventName() "{" [ attributeList() ] "}"
}

/**
 * The name of an event, should be max 256 chars ([a-zA-Z0-9_]*)
 */
void eventName() :
{
  Token t;
}
{
  t=<ID>
  {
    if ( getEventTemplateDB().addEvent(t.image))
    {
      setCurrentEvent(t.image);
    }
    else
    {
      throw new ParseException("Problem adding event "+t.image);
    }
  }
}

void attributeList() :
{}
{
  attribute() ( attribute() )*
}

String stringLiteral() :
{
    Token t;
}
{
    t = <STRING_LITERAL>
    {
       return removeQuotes(t.image);
    }
}

boolean booleanLiteral() :
{}
{
  <TRUE>
  {
    return true;
  }
  |
  <FALSE>
  {
    return false;
  }
}

int integerLiteral() :
{
    Token t;
}
{
  t = <INTEGER_LITERAL>
  {
    try {
      return Integer.parseInt(t.image);
    } catch (NumberFormatException e) {
      throw new Error();
    }
  }
}

BigInteger bigIntegerLiteral() :
{
    Token t;
}
{
  t = <BIG_INTEGER_LITERAL>
  {
    return new BigInteger(t.image);
  }
}

IPAddress ipaddrLiteral() :
{
    Token t;
}
{
  t = <IPADDR_LITERAL>
  {
    return new IPAddress(t.image);
  }
}

double doubleLiteral() :
{
    Token t;
}
{
    t = <DOUBLE_LITERAL>
    {
       try {
         return Double.parseDouble(t.image);
       } catch (NumberFormatException e) {
         throw new Error();
       }
    }
}

Object defaultValue() :
{
    Object obj;
}
{
    obj = integerLiteral()
    {
      return obj;
    }
    | obj = bigIntegerLiteral()
    {
      return obj;
    }
    | obj = doubleLiteral()
    {
      return obj;
    }
    | obj = booleanLiteral()
    {
      return obj;
    }
    | obj = stringLiteral()
    {
      return obj;
    }
    | obj = ipaddrLiteral()
    {
      return obj;
    }
}

boolean required() :
{
}
{
    <REQ>
    {
      return true;
    }
    | <OPT>
    {
      return false;
    }
}

void attribute() :
{
  FieldType aType;
  String anAttribute;
  int anArraySize = 0;
  boolean required = false;
  Object aDefaultValue = null;
}
{
  (((required = required()) (aType=type() anAttribute=attributeName() [ "[" anArraySize = integerLiteral() "]" ] [ "=" aDefaultValue = defaultValue() ] ";"))
|
  (aType=type() anAttribute=attributeName() [ "[" anArraySize = integerLiteral() "]" ] [ "=" aDefaultValue = defaultValue() ] ";")  ) {
      String evt = getCurrentEvent();
      if ( evt == null ) throw new ParseException("Bad Event");

      if (log.isTraceEnabled()) {
        log.trace("Type: "+aType+" attr: "+anAttribute+" size: "+anArraySize);
        log.trace("Required: "+required);
        log.trace("default value: "+aDefaultValue);
      }

      if (anArraySize > 0) {
        aType = aType.getArrayType();
      }

      if ( !getEventTemplateDB().addEventAttribute(evt,
                                                   anAttribute,
                                                   aType,
                                                   anArraySize,
                                                   required,
                                                   aDefaultValue))
      {
        throw new ParseException("Problem adding attribute "+evt+"("
                                 +aType+","+anAttribute+")");
      }
   }
}

FieldType type() :
{
  Token t;
}
{
  t=<ID>
  {
    return FieldType.byName(t.image);
  }
}

String attributeName() :
{
  Token t;
}
{
  t=<ID>
  {
    return t.image;
  }
}
